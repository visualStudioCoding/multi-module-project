plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

bootJar {enabled = false}
bootRun {enabled = false}

// 모든 프로젝트에 적용.
allprojects{
    group = 'com.kdn'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

// 서브 프로젝트 전체에 적용.
subprojects{
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 공통적으로 사용하는 dependencies
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.assertj:assertj-core:3.23.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'io.micrometer:micrometer-core'
        implementation("org.springframework.boot:spring-boot-starter-data-jpa")
        implementation 'org.postgresql:postgresql'
        implementation 'org.mariadb.jdbc:mariadb-java-client'
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation group: 'org.json', name: 'json', version: '20240303'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 서브 프로젝트 개별 설정
// 이곳에서 lombok 같은 dependency 를 추가 할 수 있으나
// 해당 모듈 내에서도 추가 할 수 있기때문에 프로젝트간의 관계만 명시하도록 하자.
project(':wpi') {
    bootJar {enabled = true}
    jar {enabled = false}

    dependencies {
        implementation project(':core')
    }
}
project(':gmt') {
    bootJar {enabled = true}
    jar {enabled = false}

    dependencies {
        implementation project(':core')
        implementation group: 'commons-codec', name: 'commons-codec', version: '1.16.1'
    }
}
project(':hse') {
    bootJar {enabled = true}
    jar {enabled = false}

    dependencies {
        implementation project(':core')
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    }
}
project(':scheduler') {
    bootJar {enabled = true}
    jar {enabled = false}

    dependencies {
        implementation project(':core')
    }
}
project(':core') {
    bootJar {enabled = false}
    jar {enabled = true}

    dependencies {
        implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8'
    }
}
